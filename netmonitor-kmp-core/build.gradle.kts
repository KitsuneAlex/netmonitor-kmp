@file:OptIn(ExperimentalKotlinGradlePluginApi::class, ExperimentalAbiValidation::class)

import org.jetbrains.kotlin.gradle.ExperimentalKotlinGradlePluginApi
import org.jetbrains.kotlin.gradle.dsl.JvmTarget
import org.jetbrains.kotlin.gradle.dsl.abi.ExperimentalAbiValidation
import org.jetbrains.kotlin.gradle.plugin.mpp.KotlinNativeTarget
import org.jetbrains.kotlin.gradle.plugin.mpp.NativeBuildType
import org.jetbrains.kotlin.konan.target.Family

plugins {
    alias(libs.plugins.kotlinMultiplatform)
    alias(libs.plugins.androidLibrary)
    alias(libs.plugins.mavenPublish)
    signing
}

kotlin {
    jvmToolchain(libs.versions.jvmTarget.get().toInt())
    android {
        namespace = group.toString()
        compileSdk = libs.versions.android.compileSdk.get().toInt()
        minSdk = libs.versions.android.minSdk.get().toInt()
        lint.targetSdk = libs.versions.android.targetSdk.get().toInt()
    }
    listOf(iosSimulatorArm64(), iosArm64(), linuxX64(), linuxArm64(), macosArm64(), mingwX64()).forEach {
        it.binaries.sharedLib()
    }

    jvm {
        compilerOptions {
            jvmTarget.set(JvmTarget.fromTarget(libs.versions.jvmTarget.get()))
        }
    }

    applyDefaultHierarchyTemplate {
        common {
            group("ios") { withIos() }
            group("macos") { withMacos() }
            group("native") {
                withMingw()
                withLinux()
                group("ios")
                group("macos")
            }
            group("nonAndroid") {
                withJvm()
                group("native")
            }
        }
    }

    abiValidation()

    sourceSets {
        sourceSets.all {
            compilerOptions {
                freeCompilerArgs.add("-Xcontext-parameters")
            }

            languageSettings {
                optIn("net.cacheoverflow.netmonitor.InternalNetMonitorAPI")
                optIn("kotlinx.cinterop.ExperimentalForeignApi")
            }
        }

        commonMain {
            dependencies {
                implementation(libs.compose.runtime.annotations)
                api(libs.kotlinx.coroutines.core)
            }
        }
        mingwMain {
            dependencies {
                implementation(libs.comInterop.core)
            }
        }
    }
}

val copyNativeBinariesToJar = tasks.register<Copy>("copyNativeBinariesToTar") {
    group = "natives"
    description = "Copies required native libraries to the jar output"
    into(layout.buildDirectory.dir("generated/native-resources"))
    kotlin.targets.filterIsInstance<KotlinNativeTarget>()
        .filter { it.konanTarget.family != Family.IOS }
        .forEach { target ->
            val sharedLibrary = requireNotNull(target.binaries.findSharedLib(NativeBuildType.RELEASE))
            dependsOn(sharedLibrary.linkTaskProvider)
            from(sharedLibrary.linkTaskProvider.map { it.outputFile }) {
                val arch = target.konanTarget.architecture.name.lowercase()
                val extension = sharedLibrary.outputFile.extension
                val family = when (val f = target.konanTarget.family) {
                    Family.MINGW -> "windows"
                    Family.OSX -> "macos"
                    else -> f.name.lowercase()
                }

                rename { "netmonitor-binaries/${family}_${arch}.$extension" }
            }
        }
}

kotlin {
    jvm {
        compilations.getByName("main").defaultSourceSet.resources.srcDir(copyNativeBinariesToJar)
    }
}

mavenPublishing {
    publishToMavenCentral()
    signAllPublications()
    coordinates(group.toString(), name, version.toString())

    pom {
        name = "netmonitor-kmp"
        description = " A reactive network monitoring library for Kotlin Multiplatform."
        url = "https://github.com/cach30verfl0w/netmonitor-kmp"
        licenses {
            license {
                name = "Apache License 2.0"
                url = "https://www.apache.org/licenses/LICENSE-2.0"
            }
        }
        developers {
            developer {
                id = "cach30verfl0w"
                name = "Cedric Hammes"
                url = "https://github.com/cach30verfl0w"
                email = "contact@cach30verfl0w.net"
            }
        }
        contributors {
            contributor {
                name = "KitsuneAlex"
                url = "https://karmakrafts.dev"
                email = "support@karmakrafts.dev"
            }
        }
        scm {
            url = this@pom.url
        }
    }
}

signing {
    isRequired = false
}
